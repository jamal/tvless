def javaVersion = 1.8
def javaProjects = [ project(':tvless-api'), project(':tvless-updater'), project(':tvless-notifier'),
project(':tvless-common') ]
def bootProjects = javaProjects - project(':tvless-common')

wrapper.gradleVersion = '2.9'

buildscript {
    ext.springRepo = 'http://repo.spring.io/libs-release'

    repositories {
        mavenCentral()
        maven { url springRepo }
    }


    dependencies {
        classpath 'io.spring.gradle:dependency-management-plugin:0.5.3.RELEASE'
        classpath "org.springframework.boot:spring-boot-gradle-plugin:1.3.1.RELEASE"
        classpath 'org.cloudfoundry:cf-gradle-plugin:1.1.3' // see deploy.gradle
    }
}

configure(allprojects) {
    apply plugin: 'idea'
}

configure(javaProjects) {
    apply plugin: 'java'

    targetCompatibility = javaVersion
    sourceCompatibility = javaVersion

    repositories {
        mavenCentral()
        maven { url springRepo }
    }

    dependencies {
        compile 'org.slf4j:slf4j-api'

        testCompile 'junit:junit'
        testCompile 'org.hamcrest:hamcrest-library'
        testCompile 'org.mockito:mockito-core:1.+'
        testCompile 'org.springframework:spring-test'
        testCompile 'com.jayway.jsonpath:json-path'
    }
}

configure(bootProjects) {
    apply plugin: 'io.spring.dependency-management'
    apply plugin: 'spring-boot'

    dependencyManagement {
        imports {
            mavenBom 'io.spring.platform:platform-bom:2.0.1.RELEASE'
        }
    }
}

